# 2 refs:
# https://gitlab.archlinux.org/archlinux/packaging/packages/emacs/-/blob/main/PKGBUILD
# https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=emacs-git

XWIDGETS=  # Use GTK+ widgets pulled from webkit2gtk. Usable.
NOGZ="YES" # Don't compress .el files. (Gain is neglible, IMHO)

################################################################################
pkgname="emacs-git"
pkgver=31.0.50.175326
pkgrel=1
pkgdesc='The extensible, customizable, self-documenting real-time display editor with native compilation and PGTK enabled'
arch=('x86_64')
url='https://www.gnu.org/software/emacs/emacs.html'
license=('GPL3')
depends_nox=()
depends=(
  gnutls     # NOX
  libxml2.so # NOX
  jansson    # NOX

  # libharfbuzz.so
  # libotf
  # libgccjit
  # libgtk-3.so
  # libsm
  # xcb-util # ???
  # libxcb   # ???
  # libtree-sitter.so
  # sqlite libsqlite3.so

  # .. or

  'harfbuzz' 'libotf' 'libgccjit'
  'gtk3' 'libsm' 'xcb-util' 'libxcb'
  'tree-sitter' 'sqlite3'

  # something other (from arch repos pkgbuild)
#  gmp
#  lcms2
#  libacl.so
#  libasound.so
#  libdbus-1.so
#  libfontconfig.so
#  libfreetype.so
#  libgdk-3.so
#  libgdk_pixbuf-2.0.so
#  libgif.so
#  libgio-2.0.so
#  libglib-2.0.so
#  libgobject-2.0.so
#  libgpm.so
#  libice
#  libjpeg.so
#  libncursesw.so
#  libpango-1.0.so
#  libpng
#  librsvg-2.so
#  libsystemd.so
#  libtiff.so
#  libtree-sitter.so
#  libwebp.so
#  libwebpdemux.so
#  libxfixes
#  m17n-lib
#  zlib
)

makedepends=('git' 'libgccjit' 'xorgproto' 'libxi')
provides=('emacs')
conflicts=('emacs')
source=("emacs-git::git+https://git.savannah.gnu.org/git/emacs.git")
options=(!strip)
install=emacs-git.install
b2sums=('SKIP')

if [[ $XWIDGETS == "YES" ]]; then
  depends+=('webkit2gtk')
fi

pkgver() {
  cd "$srcdir/emacs-git"

  printf "%s.%s" \
    $(grep AC_INIT configure.ac |
      awk -F',' '{ gsub("[ \\[\\]]","",$2); print $2 }') \
    $(git rev-list --count HEAD)
}

# There is no need to run autogen.sh after first checkout.
# Doing so, breaks incremental compilation.
prepare() {
  cd "$srcdir/emacs-git"
  [[ -x configure ]] || (./autogen.sh git && ./autogen.sh autoconf)
  mkdir -p "$srcdir/emacs-git/build"
}

build() {
  cd "$srcdir/emacs-git/build"

  # export ac_cv_lib_gif_EGifPutExtensionLast=yes

  local _conf=(
    # copied from arch repos pkgbuild
    --sysconfdir=/etc
    --prefix=/usr
    --libexecdir=/usr/lib
    --with-tree-sitter
    --localstatedir=/var
    --with-cairo
    --disable-build-details
    --with-harfbuzz
    --with-libsystemd
    --with-modules
    # wayland
    --with-native-compilation=aot # Compile all elisp files provided by upstream.
    --with-pgtk

    --without-xaw3d # no scroll bars

    # https://github.com/raxod502/emacs/blob/master/INSTALL#L257
    --without-gsettings
    # Beware https://debbugs.gnu.org/cgi/bugreport.cgi?bug=25228
    # dconf and gconf break font settings you set in ~/.emacs.
    # If you insist you'll need to read that bug report in *full*.
    # Good luck!
    --without-gconf

    --with-sound=no
    --without-gpm # TODO: what's that?

    # --mandir=/usr/share/man # defaults to '/usr/local/share/man/man1'.
    --without-m17n-flt

    # ctags/etags may be provided by other packages, e.g, universal-ctags 
    --program-transform-name=s/\([ec]tags\)/\1.emacs/
  )

  if [[ $XWIDGETS == "YES" ]]; then
    _conf+=('--with-xwidgets')
  fi

  if [[ $NOGZ == "YES" ]]; then
    _conf+=('--without-compress-install')
  fi

  ../configure "${_conf[@]}"

  # Using "make" instead of "make bootstrap" enables incremental
  # compiling. Less time recompiling. Yay! But you may
  # need to use bootstrap sometimes to unbreak the build.
  # Just add it to the command line.
  #
  # Please note that incremental compilation implies that you
  # are reusing your src directory!
  #

  # You may need to run this if 'loaddefs.el' files become corrupt.
  #cd "$srcdir/emacs-git/lisp"
  #make autoloads
  # cd ../build

  make
}

package() {
  cd "$srcdir/emacs-git/build"

  make DESTDIR="$pkgdir/" install

  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/ | xargs chown root:root
}
